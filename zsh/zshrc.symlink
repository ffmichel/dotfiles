# shortcut to this dotfiles path is $DOTZSH
export DOTZSH=$HOME/.dotfiles

# your project folder that we can `c [tab]` to
export PROJECTS=~/Documents/Code

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($DOTZSH/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files
# oh-my-zsh specifics
export ZSH=$HOME/.oh-my-zsh
ZSH_THEME="bullet-train/bullet-train"
BULLETTRAIN_CONTEXT_SHOW=true
BULLETTRAIN_CONTEXT_BG=white
BULLETTRAIN_CONTEXT_FG=black
# BULLETTRAIN_VIRTUALENV_PREFIX='⚡'
BULLETTRAIN_GIT_AHEAD="  %F{red}➜%F{black}"
plugins=(git tmux mosh vi-mode)
source $ZSH/oh-my-zsh.sh

source $DOTZSH/system/aliases.zsh

#virtualenvwrapper
export VIRTUALENVWRAPPER_HOOK_DIR=$DOTZSH/venv
if [ -z "${VIRTUAL_ENV}" ]; then
	source $(which virtualenvwrapper.sh)
fi

eval "$(dircolors $DOTZSH/zsh/dircolor_wombat)"

#launch tmux at startup
# if command -v tmux>/dev/null; then
	  # [[ ! $TERM =~ screen ]] && [ -z $TMUX ] && exec tmux
# fi




vim_ins_mode="%{$fg[white]%}%{$fg_bold[blue]$bg[white]%} INSERT %{$reset_color%}"
vim_cmd_mode="%{$fg[green]%}%{$fg_bold[black]$bg[green]%} COMMAND %{$reset_color%}"
vim_mode=$vim_ins_mode
function zle-line-init zle-keymap-select {
    RPS1="${${KEYMAP/vicmd/${vim_cmd_mode}}/(main|viins)/${vim_ins_mode}}"
    RPS2=$RPS1
    zle reset-prompt
}
zle -N zle-line-init
zle -N zle-keymap-select

setopt transientrprompt
